buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:1.21'
    }
}

import org.yaml.snakeyaml.DumperOptions
import org.yaml.snakeyaml.Yaml

def dumperOptions = new DumperOptions()
dumperOptions.defaultFlowStyle = DumperOptions.FlowStyle.BLOCK
def yaml = new Yaml(dumperOptions)

def createSecrets(name, data) {
    def secrets = [:]
    secrets['apiVersion'] = 'v1'
    secrets['kind'] = 'Secret'
    secrets['metadata'] = ['name': name]
    secrets['type'] = 'Opaque'
    secrets['data'] = data
    return secrets
}

task writeMysqlSecrets() {
    doLast {
        def secrets = createSecrets('mysql-secrets', [
                'password'    : "${UUID.randomUUID()}".bytes.encodeBase64().toString(),
                'rootpassword': "${UUID.randomUUID()}".bytes.encodeBase64().toString()
        ])
        new File("${projectDir}/secrets/mysql-secrets.yaml").text = yaml.dump(secrets)
    }
}

task writeMongoSecrets() {
    doLast {
        def secrets = createSecrets('mongodb-secrets', [
                'password'    : "${UUID.randomUUID()}".bytes.encodeBase64().toString(),
                'rootpassword': "${UUID.randomUUID()}".bytes.encodeBase64().toString()
        ])

        new File("${projectDir}/secrets/mongodb-secrets.yaml").text = yaml.dump(secrets)
    }
}

task writeSecrets() {
    dependsOn writeMysqlSecrets, writeMongoSecrets
}
